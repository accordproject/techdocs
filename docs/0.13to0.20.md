---
id: 0.13to0.20
title: Migrating from 0.13.*
---

Much has changed in the new `0.20` release. This guide provides step-by-step instructions to port your Accord Project templates from version `0.13` or earlier to version `0.20`.

## Update the package.json

You will first need to update the `package.json` in your template and change the version for Ergo and Cicero to `0.20` (or the corresponding `alpha` or `beta` release number):
```js
...
    "accordproject": {
        "template": "clause",
        "ergo": "0.20.0-beta.1",
        "cicero": "0.20.0-beta.1"
    }
...
```

## Templates Directory Changes

The layout of templates has changed to reflect the conceptual notion of Accord Project templates (with a grammar, a model and logic). To migrate a template directory from version `0.13` or earlier to the new `0.20` layout:
1. Rename your samples from `sample.txt` to `sample.md` (or generally any other `sample*.txt` files to use the `.md` extension)
2. Rename your template grammar from `grammar/template.tem` to `grammar/template.tem.md`
3. Rename your `lib` directory to `logic`
4. Rename your `models` directory to `model`

## Text changes

Both grammar and sample text for the templates has changed to support rich text annotations through CommonMark and a new syntax for variables. You can find complete information about the new syntax in the [CiceroMark](markup-cicero) Section of this documentation. For an existing template, you should apply the following changes.

### Text grammar changes

1. Variables should be changed from `[{variableName}]` to `{{variableName}}`
2. Formatted variables should be changed to from `[{variableName as "FORMAT"}]` to `{{variableName as "FORMAT"}}`
3. Boolean variables should be changed to use the new block syntax, from `[{"This is a force majeure":?forceMajeure}]` to `{{#if forceMajeure}}This is a force majeure{{/if}}`
4. Nested clauses should be changed to use the new block syntax, from `[{#payment}]As consideration in full for the rights granted herein...[{/payment}]` to `{{#clause payment}}As consideration in full for the rights granted herein...{{/clause}}`

:::note
1. Template text is now interpreted as CommonMark which may lead to unexpected results if your text includes characters or structure that  (e.g., `#` or `##` now become headings; or `1.` or `-` now become lists). You should review both the grammar and samples so they follow the proper [CommonMark](https://commonmark.org) rules.
2. The new lexer reserves `{{` instead of reserving `[{` which means you should avoid using `{{` in your text unless for Accord Project variables.
:::

### Text samples changes

You should ensure that any changes to the grammar text is reflected in the samples. Any change in the grammar text outside of variables should be applied to the corresponding `sample.md` files as well.

:::note
You can check that the samples and grammar are in agreement by using the `cicero parse` command.
:::

### Example of text changes

Consider the text grammar for the [late delivery and penalty]() clause:

```md
Late Delivery and Penalty.
In case of delayed delivery[{" except for Force Majeure cases,":? forceMajeure}]
[{seller}] (the Seller) shall pay to [{buyer}] (the Buyer) for every [{penaltyDuration}]
of delay penalty amounting to [{penaltyPercentage}]% of the total value of the Equipment
whose delivery has been delayed. Any fractional part of a [{fractionalPart}] is to be
considered a full [{fractionalPart}]. The total amount of penalty shall not however,
exceed [{capPercentage}]% of the total value of the Equipment involved in late delivery.
If the delay is more than [{termination}], the Buyer is entitled to terminate this Contract.
```

After applying the above rules to the code for the `0.13` version, and identifying the heading for the clause using the new markdown features, the grammar text becomes:

```hbs
## Late Delivery and Penalty.

In case of delayed delivery{{#if forceMajeure}} except for Force Majeure cases,{{/if}}
{{seller}} (the Seller) shall pay to {{buyer}} (the Buyer) for every {{penaltyDuration}}
of delay penalty amounting to {{penaltyPercentage}}% of the total value of the Equipment
whose delivery has been delayed. Any fractional part of a {{fractionalPart}} is to be
considered a full {{fractionalPart}}. The total amount of penalty shall not however,
exceed {{capPercentage}}% of the total value of the Equipment involved in late delivery.
If the delay is more than {{termination}}, the Buyer is entitled to terminate this Contract.
```

To make sure the `sample.md` file parses as well, the heading needs to be similarly identified using markdown:
```md
## Late Delivery and Penalty.

In case of delayed delivery except for Force Majeure cases,
"Dan" (the Seller) shall pay to "Steve" (the Buyer) for every 2 days
of delay penalty amounting to 10.5% of the total value of the Equipment
whose delivery has been delayed. Any fractional part of a days is to be
considered a full days. The total amount of penalty shall not however,
exceed 55% of the total value of the Equipment involved in late delivery.
If the delay is more than 15 days, the Buyer is entitled to terminate this Contract.
```

## Model changes

There is no model change required for this version.

## Logic Changes

Version `0.20` of Ergo has a few non backward compatible changes that may require you to change your template logic. The main such change is the new support for enumerated values.

### Enumerated Values

Enumerated values are now proper values with a proper enum type, not based on the type `String`.

For instance, if you are consider the enum:
```
enum Gender {
  o MALE
  o FEMALE
  o OTHER
  o UNKNOWN
}
```

In version `0.13` or earlier the Ergo code would write `"MALE"` for an enum value and treat the type `Gender` as if it was the type `String`. As of version `0.20` Ergo uses `MALE` for the same enume value and the type `Gender` is distinct from the type `String`. If you compile Ergo logic written for version `0.13` or earlier, the compiler will throw type errors in those cases. You should apply the following changes:

1. Remove the quotes (`"`) around enum values E.g., `"USD"` should now be replaced by `USD` for monetary amounts.
3. If enum values are bound to variables with a type annotation, you should change the type annotation from `String` to the correct enum type.
3. If enum values are passed as parameters in clauses or functions, you should change the type annotation for that parameter from `String` to the correct enum type.
4. In a few cases the same enumerated value may be used in different enum types (e.g., `days` and `weeks` are used in both `TemporalUnit` and `PeriodUnit`). Those two values will now have different types. If you need to distinguish, you can use the fully qualified name for the enum value (e.g., `~org.accordproject.time.TemporalUnit.days` or `~org.accordproject.time.PeriodUnit.days`).

### Other changes

1. `now` used to return the current time but is treated in `0.20` like any other variables. If your logic used the variable `now` without declaring it, this will raise a `Variable now not found` error. You should change your logic to use the `now()` function instead.

### Example of logic changes

Consider the Ergo logic for the [acceptance of delivery](https://templates.accordproject.org/acceptance-of-delivery@0.12.1.html) clause. Applying the above rules to the code for the `0.13` version:

```ergo
  clause acceptanceofdelivery(request : InspectDeliverable) : InspectionResponse {

    let received = request.deliverableReceivedAt;
    enforce isBefore(received,now) else
      throw ErgoErrorResponse{ message : "Transaction time is before the deliverable date." }
    ;

    let dur = 
      Duration{
        amount: contract.businessDays,
        unit: "days"
      };
    let status =
      if isAfter(now(), addDuration(received, dur))
      then "OUTSIDE_INSPECTION_PERIOD"
      else if request.inspectionPassed
      then "PASSED_TESTING"
      else "FAILED_TESTING"
    ;
    return InspectionResponse{
      status : status,
      shipper : contract.shipper,
      receiver : contract.receiver
    }
  }
```

results in the following new logic for the `0.20` version:

```ergo
  clause acceptanceofdelivery(request : InspectDeliverable) : InspectionResponse {

    let received = request.deliverableReceivedAt;
    enforce isBefore(received,now()) else                // changed to now()
      throw ErgoErrorResponse{ message : "Transaction time is before the deliverable date." }
    ;

    let dur = 
      Duration{
        amount: contract.businessDays,
        unit: ~org.accordproject.time.TemporalUnit.days  // enum value with fully qualified name
      };
    let status =
      if isAfter(now(), addDuration(received, dur))      // changed to now()
      then OUTSIDE_INSPECTION_PERIOD                     // enum value has no quotes
      else if request.inspectionPassed
      then PASSED_TESTING                                // enum value has no quotes
      else FAILED_TESTING                                // enum value has no quotes
    ;
    return InspectionResponse{
      status : status,
      shipper : contract.shipper,
      receiver : contract.receiver
    }
  }
```
